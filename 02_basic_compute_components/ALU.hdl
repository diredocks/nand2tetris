// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: p
/**
 * ALU (Arithmetic Logic Unit):
 * Computes out = one of the following functions:
 *                0, 1, -1,
 *                x, y, !x, !y, -x, -y,
 *                x + 1, y + 1, x - 1, y - 1,
 *                x + y, x - y, y - x,
 *                x & y, x | y
 * on the 16-bit inputs x, y,
 * according to the input bits zx, nx, zy, ny, f, no.
 * In addition, computes the two output bits:
 * if (out == 0) zr = 1, else zr = 0
 * if (out < 0)  ng = 1, else ng = 0
 */
// Implementation: Manipulates the x and y inputs
// and operates on the resulting values, as follows:
// if (zx == 1) sets x = 0        // 16-bit constant
// if (nx == 1) sets x = !x       // bitwise not
// if (zy == 1) sets y = 0        // 16-bit constant
// if (ny == 1) sets y = !y       // bitwise not
// if (f == 1)  sets out = x + y  // integer 2's complement addition
// if (f == 0)  sets out = x & y  // bitwise and
// if (no == 1) sets out = !out   // bitwise not

CHIP ALU {
    IN  
        x[16], y[16],  // 16-bit inputs        
        zx, // zero the x input?
        nx, // negate the x input?
        zy, // zero the y input?
        ny, // negate the y input?
        f,  // compute (out = x + y) or (out = x & y)?
        no; // negate the out output?
    OUT 
        out[16], // 16-bit output
        zr,      // if (out == 0) equals 1, else 0
        ng;      // if (out < 0)  equals 1, else 0

    PARTS:
    // get desired x and y
    Mux16(a=x , b=false , sel=zx , out=ifxzero );
    Not16(in=ifxzero , out=ifnotx );
    Mux16(a=ifxzero , b=ifnotx , sel=nx , out=finalx );
    Mux16(a=y , b=false , sel=zy , out=ifyzero );
    Not16(in=ifyzero , out=ifnoty );
    Mux16(a=ifyzero , b=ifnoty , sel=ny , out=finaly );
    // doing the calc and output
    Add16(a=finalx , b=finaly , out=addout );
    And16(a=finalx , b=finaly , out=andout );
    Mux16(a=andout , b=addout , sel=f , out=tmpout );
    Not16(in=tmpout , out=notout );
    Mux16(a=tmpout , b=notout , sel=no , 
          out[0..7]=l, out[8..15]=h, out[15]=ng, out=out );
    // chores value
    Or8Way(in=l , out=f0l);
    Or8Way(in=h , out=f0h);
    Or(a=f0h , b=f0l , out=nzr );
    Not(in=nzr , out=zr );
}